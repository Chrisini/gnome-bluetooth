AC_INIT(libegg, 0.0.1)
AC_CONFIG_SRCDIR(libegg/util/eggmarshalers.list)
## AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)
AM_INIT_AUTOMAKE

AM_CONFIG_HEADER(config.h)

AM_MAINTAINER_MODE

AC_PROG_CC
AC_ISC_POSIX
AC_HEADER_STDC
AM_PROG_LIBTOOL

changequote(,)dnl
if test "x$GCC" = "xyes"; then
  case " $CFLAGS " in
  *[\ \	]-Wall[\ \	]*) ;;
  *) CFLAGS="$CFLAGS -Wall" ;;
  esac

  if test "x$enable_ansi" = "xyes"; then
    case " $CFLAGS " in
    *[\ \	]-ansi[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -ansi" ;;
    esac

    case " $CFLAGS " in
    *[\ \	]-pedantic[\ \	]*) ;;
    *) CFLAGS="$CFLAGS -pedantic" ;;
    esac
  fi
fi
changequote([,])dnl

## Joke from Anders, but he didn't have the bad taste to commit to CVS ;-)
AC_MSG_CHECKING([for chicken])
AC_MSG_RESULT([must have egg first])

AC_PATH_XTRA

# Check return type of signal
AC_TYPE_SIGNAL

## here we get the flags we'll actually use
PKG_CHECK_MODULES(EGG, gtk+-2.0 >= 2.0.0)
AC_SUBST(EGG_LIBS)
AC_SUBST(EGG_CFLAGS)

PKG_CHECK_MODULES(EGG_RECENT, gtk+-2.0 >= 2.0.0 gnome-vfs-2.0 libbonoboui-2.0 librsvg-2.0 libgnomeui-2.0)
AC_SUBST(EGG_RECENT_LIBS)
AC_SUBST(EGG_RECENT_CFLAGS)

PKG_CHECK_MODULES(EGG_EXEC, libglade-2.0 libgnome-2.0 >= 2.0.0 gtk+-2.0 >= 2.0.0)
AC_SUBST(EGG_EXEC_LIBS)
AC_SUBST(EGG_EXEC_CFLAGS)

PKG_CHECK_MODULES(EGG_VFS_FS, gnome-vfs-2.0 libgnomeui-2.0)
AC_SUBST(EGG_VFS_FS_LIBS)
AC_SUBST(EGG_VFS_FS_CFLAGS)

PKG_CHECK_MODULES(EGG_VFS_MODULE, gnome-vfs-module-2.0)
AC_SUBST(EGG_VFS_MODULE_LIBS)
AC_SUBST(EGG_VFS_MODULE_CFLAGS)

PKG_CHECK_MODULES(EGG_XML, libxml-2.0)
AC_SUBST(EGG_XML_LIBS)
AC_SUBST(EGG_XML_CFLAGS)

PKG_CHECK_MODULES(EGG_TRAY, gtk+-2.0 >= 2.2.0 libgnomeui-2.0 >=  2.2.0)
AC_SUBST(EGG_TRAY_LIBS)
AC_SUBST(EGG_TRAY_CFLAGS)

## check if we have gtk+ 2.1.x
AM_CONDITIONAL(HAVE_GTK21, $PKG_CONFIG --atleast-version=2.1.0 gtk+-2.0)

dnl
dnl check if we have gtk+ 2.1.x (again - for multihead support)
dnl
AC_MSG_CHECKING([for gtk+ multihead support])
if $PKG_CONFIG --atleast-version 2.1.0 gtk+-2.0; then
   AC_DEFINE(HAVE_GTK_MULTIHEAD,,[gtk+ with multihead support found])
   AC_MSG_RESULT(yes)
else
   AC_MSG_RESULT(no)
fi

GLIB_GENMARSHAL=`$PKG_CONFIG --variable=glib_genmarshal glib-2.0`  
AC_SUBST(GLIB_GENMARSHAL)

GLIB_MKENUMS=`$PKG_CONFIG --variable=glib_mkenums glib-2.0`  
AC_SUBST(GLIB_MKENUMS)

# Honor aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

AC_ARG_WITH(html-dir, [  --with-html-dir=PATH path to installed docs ])
if test "x$with_html_dir" = "x" ; then
  HTML_DIR='${datadir}/gtk-doc/html'
else
  HTML_DIR=$with_html_dir
fi
AC_SUBST(HTML_DIR)
AM_CONDITIONAL(ENABLE_GTK_DOC, true)



AC_OUTPUT([
Makefile
libegg/Makefile
libegg/util/Makefile
libegg/iconlist/Makefile
libegg/tray/Makefile
])
